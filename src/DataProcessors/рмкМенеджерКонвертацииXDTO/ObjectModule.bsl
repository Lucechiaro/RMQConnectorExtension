#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьДоступныеМетоды(Направление) Экспорт
	
	ИменаМетодов = Новый Массив;
	
	Если Направление = "Входящее" Тогда
		ИменаМетодов.Добавить("ВходящиеСообщения_Прочитать");
	ИначеЕсли Направление = "Исходящее" Тогда
		ИменаМетодов.Добавить("ИсходящиеСообщения_Сформировать");
	ИначеЕсли Направление = "ИдентификаторыПолучателей" Тогда
		ИменаМетодов.Добавить("ИдентификаторыПолучателей_НоменклатураДляСкладов");
	КонецЕсли;
	
	Возврат ИменаМетодов;
	
КонецФункции

Процедура ВыполнитьМетод(ИмяМетода, Параметры) Экспорт
	
	Если ИмяМетода = "ИсходящиеСообщения_Сформировать" Тогда
		ИсходящиеСообщения_Сформировать(Параметры);
	ИначеЕсли ИмяМетода = "ВходящиеСообщения_Прочитать" Тогда
		ВходящиеСообщения_Прочитать(Параметры);
	ИначеЕсли ИмяМетода = "ИдентификаторыПолучателей_НоменклатураДляСкладов" Тогда
		ИдентификаторыПолучателей_НоменклатураДляСкладов(Параметры);	
	Иначе
		ВызватьИсключение СтрШаблон("Не найден метод конвертации %1", ИмяМетода);
	КонецЕсли;	
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИсходящиеСообщения_Сформировать(Параметры)
	
	ОбъектДляСериализации	= Параметры.ОбъектДляСериализации;
	ДанныеСообщения 		= Параметры.ДанныеСообщения;
	
	Если рмкОбщегоНазначения.ЭтоДокумент(ОбъектДляСериализации) И Параметры.Свойство("UnloadRegisterRecords") Тогда
		
		ПрочитатьДвиженияВДокумент(ОбъектДляСериализации);
		ДанныеСообщения.Вставить("UnloadRegisterRecords");
		
	КонецЕсли;
	
	// задействуем платформенную сериализацию
	ДанныеСообщения.Вставить("SerializationMetod", 	"SerializationMetod_1CXDTO");
	ДанныеСообщения.Вставить("Content", 			ОбъектДляСериализации);
	
КонецПроцедуры	

Процедура ПрочитатьДвиженияВДокумент(ДокументОбъект)
	
	Для Каждого НаборДвижений Из ДокументОбъект.Движения Цикл
		НаборДвижений.Прочитать();
	КонецЦикла	
	
КонецПроцедуры		

Процедура ВходящиеСообщения_Прочитать(КонтейнерСообщения)
	
	Объект = КонтейнерСообщения.Content;
	УстановитьДополнительныеПараметрыЗаписиОбъета(Объект);
	Объект.Записать();
	
	ДобавитьДокументКОтложенномуПроведению = рмкОбщегоНазначения.ЭтоДокумент(Объект) И Объект.Проведен И Не КонтейнерСообщения.Свойство("UnloadRegisterRecords");
	
	Если ДобавитьДокументКОтложенномуПроведению Тогда
		РегистрыСведений.рмкДокументыКОтложенномуПроведению.ДобавитьВОчередь(Объект.Ссылка);
	КонецЕсли;	
	
КонецПроцедуры

Процедура УстановитьДополнительныеПараметрыЗаписиОбъета(ЗаписываемыйОбъект)
	
	ЗаписываемыйОбъект.ОбменДанными.Загрузка = Истина;
	ЗаписываемыйОбъект.ДополнительныеСвойства.Вставить("рмкНеРегистрироватьКОбмену");
		
КонецПроцедуры	

// в параметрах ожидаем структуру в составе
// "Объект" - сериализуемый объект
// "МассивПолучателей" - пустой массив, который мы должны заполнить в этом методе
Процедура ИдентификаторыПолучателей_НоменклатураДляСкладов(Параметры)
	
	// пример
	Параметры.МассивПолучателей.Добавить("Stock1");
	Параметры.МассивПолучателей.Добавить("Stock2");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли