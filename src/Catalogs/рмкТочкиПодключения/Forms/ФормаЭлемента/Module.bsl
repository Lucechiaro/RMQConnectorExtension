
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Объект.МенеджерКонвертации.Пустая() Тогда
		ЗаполнитьОбработчики(ЭтотОбъект);
	КонецЕсли;
	
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаполнитьУзелОбмена(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МенеджерКонвертацииПриИзменении(Элемент)
	
	Объект.ИмяМетодаКонвертации = "";
	ЗаполнитьОбработчики(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьМетаданныеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыборМетаданныхПродолжение = Новый ОписаниеОповещения("ВыборМетаданныхПродолжение", ЭтотОбъект);
	ВыбранныеМетаданные = КолонкиТаблицыВМассивСтруктур(Объект.МетаданныеДляРепликации, "ПолноеИмя,ВыгружатьСДвижениями");
	ПараметрыФормы = Новый Структура("ВыбранныеМетаданные", ВыбранныеМетаданные);
	
	ОткрытьФорму("Справочник.рмкТочкиПодключения.Форма.ФормаПодбораМетаданных", ПараметрыФормы, ЭтотОбъект, 
					, , , ВыборМетаданныхПродолжение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипИнформацииПриИзменении(Элемент)
	
	УстановитьВидимостьПоТипуИнформации(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверятьПоПравиламРегистрацииПриИзменении(Элемент)
	
	УстановитьВидимостьУзлаПланаОбмена(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьДляМножестваПолучателейПриИзменении(Элемент)
	
	Если Объект.ИспользоватьДляМножестваПолучателей Тогда
		Объект.ПроверятьПоПравиламРегистрации = Ложь;
	КонецЕсли;
	
	УстановитьВидимостьИсточникПолучателей(ЭтотОбъект);
	УстановитьВидимостьМетодаСпискаПолучателей(ЭтотОбъект);
	УстановитьВидимостьПроверятьПоПравиламРегистрации(ЭтотОбъект);
	УстановитьВидимостьУзлаПланаОбмена(ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура УзелОбменаПриИзменении(Элемент)
	
	УзелОбменаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсточникСпискаПолучателейПриИзменении(Элемент)
	
	УстановитьВидимостьУзлаПланаОбмена(ЭтотОбъект);
	УстановитьВидимостьМетодаСпискаПолучателей(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДоступность(Форма)
	
	УстановитьНадписьМетаданные(Форма);
	УстановитьВидимостьПоТипуИнформации(Форма);
	УстановитьВидимостьУзлаПланаОбмена(Форма);
	УстановитьВидимостьИсточникПолучателей(Форма);
	УстановитьВидимостьМетодаСпискаПолучателей(Форма);
	УстановитьВидимостьПроверятьПоПравиламРегистрации(Форма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьМассивОбработчиков(СпособСериализации, ТипОбработчиков) 
		
	Попытка
		
		МенеджерКонвертации = рмкОбработкаСообщений.ПолучитьМенеджерКонвертации(Неопределено, СпособСериализации);
		МассивОбработчиков = МенеджерКонвертации.ПолучитьДоступныеМетоды(ТипОбработчиков)
		
	Исключение
		
		МассивОбработчиков = Новый Массив;
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		рмкОбщегоНазначенияПереопределяемый.СообщитьПользователю(ОписаниеОшибки);
				
	КонецПопытки;
	
	Возврат МассивОбработчиков;
		
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьОбработчики(Форма)
	
	ЗаполнитьОбработчикиПоТипу(Форма, "ИмяМетодаКонвертации", "Исходящее");
	ЗаполнитьОбработчикиПоТипу(Форма, "ИмяМетодаСпискаПолучателей", "ИдентификаторыПолучателей");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьОбработчикиПоТипу(Форма, ИмяЭлемента, ТипОбработчиков)
	
	ЭлементФормы = Форма.Элементы.Найти(ИмяЭлемента);
	ЭлементФормы.СписокВыбора.Очистить();
	Обработчики = ПолучитьМассивОбработчиков(Форма.Объект.МенеджерКонвертации, ТипОбработчиков);
	
	Для Каждого ИмяОбработчика Из Обработчики Цикл
		ЭлементФормы.СписокВыбора.Добавить(ИмяОбработчика);
	КонецЦикла;	

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КолонкиТаблицыВМассивСтруктур(Таблица, ИменаКолонок)
	
	Результат = Новый Массив; 
	
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		
		СтруктураДанных = Новый Структура(ИменаКолонок);
		ЗаполнитьЗначенияСвойств(СтруктураДанных, СтрокаТаблицы);
		Результат.Добавить(СтруктураДанных);
		
	КонецЦикла;
		
	Возврат Результат;	
	
КонецФункции	

&НаКлиентеНаСервереБезКонтекста
Функция КолонкуТаблицыВМассив(Таблица, ИмяКолонки)
	
	Результат = Новый Массив; 
	
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		Результат.Добавить(СтрокаТаблицы[ИмяКолонки]);
	КонецЦикла;
		
	Возврат Результат;	
	
КонецФункции

&НаКлиенте
Процедура ВыборМетаданныхПродолжение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		
		Объект.МетаданныеДляРепликации.Очистить();
		
		Для Каждого ОписаниеМетаданных Из РезультатВыбора Цикл
			ЗаполнитьЗначенияСвойств(Объект.МетаданныеДляРепликации.Добавить(), ОписаниеМетаданных);
		КонецЦикла;	
		
		УстановитьНадписьМетаданные(ЭтотОбъект);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьНадписьМетаданные(Форма)
	
	КоличествоЭлементов = Форма.Объект.МетаданныеДляРепликации.Количество();
	ПредставлениеМетаданных = СтрСоединить(КолонкуТаблицыВМассив(Форма.Объект.МетаданныеДляРепликации, "ПолноеИмя"), ",");
	ПредельнаяДлинаОписания = 75;
	
	Если СтрДлина(ПредставлениеМетаданных) > ПредельнаяДлинаОписания Тогда
		ПредставлениеМетаданных = Лев(ПредставлениеМетаданных, ПредельнаяДлинаОписания - 3) + "...";
	КонецЕсли;	
		
	Форма.НадписьМетаданные = СтрШаблон("Метаданные (%1)", КоличествоЭлементов);
	
	Если Не ПустаяСтрока(ПредставлениеМетаданных) Тогда
		Форма.НадписьМетаданные = Форма.НадписьМетаданные + ": " + ПредставлениеМетаданных;
	КонецЕсли;		
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьПоТипуИнформации(Форма)
	
	Если Форма.Объект.ТипИнформации = ПредопределенноеЗначение("Перечисление.рмкТипыПередаваемойИнформации.Логи") Тогда
		
		Форма.Элементы.МенеджерКонвертации.Видимость 	= Ложь;
		Форма.Элементы.ИмяМетодаКонвертации.Видимость 	= Ложь;
		Форма.Элементы.НадписьМетаданные.Видимость 		= Ложь;
		Форма.Элементы.ИмяДляРазработчика.Видимость 	= Истина;
		
	Иначе
		
		Форма.Элементы.МенеджерКонвертации.Видимость 	= Истина;
		Форма.Элементы.ИмяМетодаКонвертации.Видимость 	= Истина;
		Форма.Элементы.НадписьМетаданные.Видимость 		= Истина;
		Форма.Элементы.ИмяДляРазработчика.Видимость 	= Ложь;
		 		
	КонецЕсли;	 		
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьУзлаПланаОбмена(Форма)
	
	СписокПолучателейИзБеремИзУзла = Форма.Объект.ИсточникСпискаПолучателей = ПредопределенноеЗначение("Перечисление.рмкИсточникиСписковПолучателей.ПланОбмена")
						Или Форма.Объект.ИсточникСпискаПолучателей = ПредопределенноеЗначение("Перечисление.рмкИсточникиСписковПолучателей.ПланОбменаСПравиламиРегистрации");
	
	Форма.Элементы.УзелОбмена.Видимость = Форма.Объект.ПроверятьПоПравиламРегистрации
		Или (Форма.Объект.ИспользоватьДляМножестваПолучателей И СписокПолучателейИзБеремИзУзла);
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьУзелОбмена(ТекущийОбъект)
	
	Если Не ПустаяСтрока(ТекущийОбъект.ИмяПланаОбмена) И ЗначениеЗаполнено(ТекущийОбъект.УИДУзлаОбмена) Тогда
		УзелОбмена = ПланыОбмена[ТекущийОбъект.ИмяПланаОбмена].ПолучитьСсылку(ТекущийОбъект.УИДУзлаОбмена);	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УзелОбменаПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(УзелОбмена) Тогда
		
		Объект.ИмяПланаОбмена = УзелОбмена.Метаданные().Имя;
		Объект.УИДУзлаОбмена = УзелОбмена.УникальныйИдентификатор();
		
	Иначе
			
		Объект.ИмяПланаОбмена = "";
		Объект.УИДУзлаОбмена = Неопределено;
			
	КонецЕсли;		
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьИсточникПолучателей(Форма)
	
	Форма.Элементы.ИсточникСпискаПолучателей.Видимость = Форма.Объект.ИспользоватьДляМножестваПолучателей;
	Форма.Элементы.ОдноСообщениеДляМножестваПолучателей.Видимость = Форма.Объект.ИспользоватьДляМножестваПолучателей;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьМетодаСпискаПолучателей(Форма)
	
	Форма.Элементы.ИмяМетодаСпискаПолучателей.Видимость = Форма.Объект.ИспользоватьДляМножестваПолучателей
		И Форма.Объект.ИсточникСпискаПолучателей = ПредопределенноеЗначение("Перечисление.рмкИсточникиСписковПолучателей.МенеджерКонвертации");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьПроверятьПоПравиламРегистрации(Форма)
	
	Форма.Элементы.ПроверятьПоПравиламРегистрации.Видимость = Не Форма.Объект.ИспользоватьДляМножестваПолучателей;
	
КонецПроцедуры

#КонецОбласти